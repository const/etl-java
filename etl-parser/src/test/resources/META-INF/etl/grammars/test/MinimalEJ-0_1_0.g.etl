doctype ETL.Grammar "0.3.0";
/// The statement below is a special blank statement to ensure that it is ignored.
;
/// This is almost minimal grammar that supports HelloWorld.ej.etl
/// But there are still some features to remove.
grammar test.MinimalEJ "0.1.0" {
	namespace default ej = "http://etl.sf.net/2006/samples/ej/0.1";

	/// A common context that defines documentation and attributes
	context Common {
		/// Simple documentation
		documentation Documentation {
			@ Documentation += doclines wrapper ej:DocumentationLine.Text;
		};

		/// Simple attributes definition
		attributes Attributes {
			@ AttributeSets += {
				^ ej:AttributeSet {
					% @ {
						@ Attributes += list , {
							expression(Expression,precedence=100);
						};
					};
				};
			}+;
		};

		def IdentifierDef {
			^ ej:Identifier {
				@ Value = identifier;
			};
		};
	};

	context abstract Classifiers {
		include Common;

		statement ClassStatement {
			% class;
			modifiers wrapper ej:Modifier.Value {
				@ VisibilityModifier = modifier public;
			};
			@ Name = ref(IdentifierDef);
			@ Contents += block(ClassContents);
		};
	};

	/// Top level context that contains package and class definitions
	context default TopLevel {
		include Classifiers wrapper ej:TopLevelClassifier.Classifier;

		statement PackageStatement {
			% package;
			@ Name = expression(PackageNameExpression);
		};


		statement BlankTopLevel {
		};
	};


	context PackageNameExpression {
		include Common;

		op composite Identifier(f) {
			ref(IdentifierDef);
		};

		op AccessOp(yfx, 100, . ) {
			@ Accessed = left;
			@ Feature = right;
		};
	};


	context TypeExpression {
		include PackageNameExpression;

		op composite VoidType(f) {
			^ ej:PrimitiveType {
				@ Name = token(void);
			};
		};

		op composite ArrayType(f) {
			^ ej:PrimitiveType {
				@ Name = token(array);
			};
		};

		op composite ApplySquareOp(yf, 100) {
			@ Functor = left;
			@ Args += % [ {
				list , {
					expression;
				};
			} % ];
		};

	};

	context Expression {
		include TypeExpression;

		op composite StringLiteral(f) {
			@ Value = string(quote='\"');
		};

		op composite ApplyRoundOp(yf, 100) {
			@ Functor = left;
			@ Args += % ( {
				list , {
					expression;
				};
			} %);
		};
	};

	context Code {
		statement ExpressionStatement {
			@ Expression = expression(Expression);
		};
		statement BlankCode {
		};
	};

	context ClassContents {
		include Common;

		statement MethodStatement {
			% to;
			modifiers wrapper ej:Modifier.Value {
				@ VisibilityModifier = modifier public;
				@ StaticModifier = modifier static;
			};
			@ ReturnType = expression(TypeExpression);
			@ Name = ref(IdentifierDef);
			@ Parameters += % ( {
				list , {
					^ ej:Parameter {
						@ Classifier = expression(TypeExpression);
						@ Name = ref(IdentifierDef);
					};
				}?;
			} % );
			@ Body = ref(MethodBlock);
		};

		def MethodBlock {
			^ ej:MethodBlock {
				@ Content += block(Code);
			};
		};

		statement BlankClassContent {
		};
	};
};